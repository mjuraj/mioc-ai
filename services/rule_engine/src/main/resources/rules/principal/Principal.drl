package rules.principal;

import java.util.List;
import com.mindsmiths.ruleEngine.model.Heartbeat;
import com.mindsmiths.ruleEngine.util.Log;
import com.mindsmiths.ruleEngine.util.Agents;
import com.mindsmiths.sdk.utils.Utils;
import com.mindsmiths.gpt3.completion.GPT3Completion;
import config.Settings;
import utils.CronTime;

import signals.SummaryReadyMessage;
import signals.RequestSummary;

import agents.Principal;


rule "Request summary data from database"
    when
        // passed time = 24h
        Heartbeat(ts: timestamp,
                  CronTime.isSatisfied("* 0 8 ? * MON *", Utils.now())) from entry-point "signals"
        agent: Principal(lastEmailSent: lastEmailSentTime,
            lastEmailSentTime == null || lastEmailSentTime before[24h] ts
        )
    then
        Log.info("Requesting new data from the latest feedback round for a new summary mail.");
        agent.send("SMITH", new RequestSummary(lastEmailSent));
        modify(agent) { setLastEmailSentTime(ts); }
end

rule "Summary is ready for GPT processing"
    when
        signal: SummaryReadyMessage() from entry-point "signals"
        agent: Principal(textSummaryBuilding == false)
    then
        Log.info("Received summary; ready for GPT processing...");
        modify(agent) { setTextSummaryBuilding(true); }
        agent.requestTextSummary(signal);
        delete(signal);
end

rule "Send email when summary text is ready"
    when
        gpt3Result: GPT3Completion() from entry-point "signals"
        agent: Principal(textSummaryBuilding == true)
    then
        modify(agent) { setTextSummaryBuilding(false); }
        String response = gpt3Result.getBestResponse();
        agent.sendEmail(response);
        delete(gpt3Result);
end